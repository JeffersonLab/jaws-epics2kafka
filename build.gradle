plugins {
    id 'java-library'
    id 'maven-publish'
    id 'distribution'
}

description "JLab Kafka Connect transform from epics2kafka to JAWS"
group 'org.jlab'
version '2.2.0'

tasks.withType(JavaCompile) {
    options.release = 8
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()
    maven {
        url "https://packages.confluent.io/maven/"
    }
}

dependencies {
    implementation 'org.apache.kafka:connect-transforms:3.0.0',
            'org.apache.avro:avro:1.10.2',
            'io.confluent:kafka-connect-avro-data:7.0.1',
            'org.jlab:jaws-libj:1.9.0'
    testImplementation 'junit:junit:4.12'
}

build {
    dependsOn(installDist)
}

distributions {
    main {
        contents {
            from jar
            from(configurations.runtimeClasspath)
            include 'jaws-epics2kafka-*'
            include 'jaws-libj-*'
        }
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "slominskir"
            name "Ryan Slominski"
            email "ryans@jlab.org"
        }
    }

    scm {
        url "https://github.com/JeffersonLab/jaws-epics2kafka"
    }
}

publishing {
    publications {
        ProjectPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.description)
                root.appendNode('name', project.name)
                root.children().last() + pomConfig
            }
        }
    }
}